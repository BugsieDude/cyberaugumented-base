Class DCY_ABarrelofExplosives : ExplosiveBarrel
{
  Mixin DCY_FX;
  
  Default
  {
  Tag "Explosive Barrel";
  DeathSound "";
  }
  
  States
  {
  Spawn:
    _B1R ABCDEFGHIJKLM 2
	{
		if (DCY_EffectDistance(900))
		{
			if (waterlevel <= 0)
			{
				if ((level.time % 3) == 0)
					A_SpawnParticleEx(0x52d124, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE|SPF_ROLL, 100, 40, 0, frandom(-8, 8), frandom(-8, 8), 25, frandom(-0.1, 0.1), frandom(-0.1, 0.1), frandom(0.3, 1), startalphaf: 0.09, fadestepf: 0.0009, sizestep: 0.25, startroll: frandom(0, 360), rollvel: random(0, 1) ? 0.1 : -0.1);
				
				if ((level.time % random(2, 9)) == 0)
					A_SpawnParticleEx(0x194d05, TexMan.CheckForTexture("graphics/particles/dcysquare.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, 65, random(3, 6), 0, frandom(-10, 10), frandom(-10, 10), 25 + frandom(-0.35, 0.35), frandom(-0.1, 0.1), frandom(-0.1, 0.1), frandom(0.05, 0.2), frandom(-0.1, 0.1), frandom(-0.1, 0.1), frandom(0.1, 0.2), fadestepf: 0, sizestep: -0.115);
			}
		}
	}
    Loop;
  Death:
	_B1R AB 2;
    _BXP A 2;
    _BXP B 2 A_Scream();
    _BXP C 3;
    _BXP D 3
	{
		A_StartSound("DCYExplodingBarrel", CHAN_BODY);
		for (int i = random(2, 4); i > 0; i--) A_SpawnItemEx("DCY_Boom", frandom(-20, 20), frandom(-20, 20), frandom(10, 30), flags: SXF_NOCHECKPOSITION);
		A_Explode();
		
		for (int i = random(4, 10); i > 0; i--)
				A_SpawnItemEx("DCY_BarrelPart", frandom(-20, 20), frandom(-20, 20), frandom(5, 56), (vel.x / 4) + frandom(-12, 12), (vel.y / 4) + frandom(-12, 12), (vel.z / 4) + frandom(1, 12), frandom(0, 360), SXF_NOCHECKPOSITION|SXF_ABSOLUTEVELOCITY, 50);
		
		A_QuakeEx(2, 2, 2, 80, 0, 500, "none", QF_RELATIVE|QF_SCALEDOWN, falloff: 800);
	}
    NBKL DEFGHIJKLM 2
	{
		A_SetRenderStyle(1.0, STYLE_ADD);
		A_SetScale(frandompick(-0.85, 0.85), 0.85);
	}
    TNT1 A 1050 A_BarrelDestroy();
    TNT1 A 5
	{
		scale = (1, 1);
		A_SetRenderStyle(1.0, STYLE_NORMAL);
		A_Respawn();
	}
    Wait;
  }
}