Class DCY_Intervention : DCY_Weapon
{
  Default
  {
	Tag "Intervention Y0";
	Weapon.SelectionOrder 2800;
	Weapon.SlotNumber 5;
	+WEAPON.NOAUTOFIRE;
	+WEAPON.EXPLOSIVE;
	Weapon.AmmoGive 2;
	Weapon.AmmoUse 1;
	Weapon.AmmoType "DCY_RocketAmmo";
	Inventory.PickupSound "dcy/rocketpickup";
	Inventory.PickupMessage "You got the Intervention Y0! Handy when you're trying to do sneak attacks.";
  }
  
  action void A_CyberaugumentedGrenade()
  {
	invoker.DepleteAmmo(invoker.bAltFire, true);
	A_StartSound("Grenade/Launch", CHAN_WEAPON, 0, 0.85, 0.85);
	A_FireProjectile("DCY_PlayerGrenade", 0, 0, pitch: -7.5);
	A_QuakeEx(1, 1, 1, 25, 0, 150, "none", QF_RELATIVE|QF_SCALEDOWN, falloff: 295);
	A_WeaponOffset(frandom(-4.5, 4.5), frandom(0, 4.35), WOF_ADD|WOF_INTERPOLATE);
	A_GunFlash();
  }
  
  States
  {
  Spawn:
	GRN_ Z -1;
	Stop;
  OverlayIdle:
	GRN_ A 1;
	Stop;
  OverlayFire:
	GRN_ AAA 1
	{
		invoker.lower += 0.75;
		A_WeaponOffset(0, 32 + invoker.lower, WOF_INTERPOLATE);
		DCY_HandleScale(5);
	}
	GRN_ AA 1
	{
		invoker.lower += 3.75;
		A_WeaponOffset(0, 32 + invoker.lower, WOF_INTERPOLATE);
		DCY_HandleScale(5);
	}
	GRN_ ABCDEFGHIJKLMNO 1
	{
		if (invoker.lower > 0.01) invoker.lower *= 0.45; else invoker.lower = 0;
		A_WeaponOffset(0, 32 + invoker.lower, WOF_INTERPOLATE);
		DCY_HandleScale(5);
	}
	Stop;
  OverlayMuzzle:
	MUZ_ AA 1 Bright
	{
		invoker.t += 0.1;
		A_OverlayRotate(-1, frandom(0, 360));
		A_OverlayScale(-1, invoker.t, invoker.t);
	}
	Stop;
  Feel:
	TNT1 A 1
	{
		A_QuakeEx(1, 1, 1, 15, 0, 450, "none", QF_RELATIVE|QF_SCALEDOWN|QF_3D);
		A_ZoomFactor(0.9);
	}
	TNT1 A 1 A_ZoomFactor(0.925);
	TNT1 A 1 A_ZoomFactor(0.9475);
	TNT1 A 1 A_ZoomFactor(0.96);
	TNT1 A 1 A_ZoomFactor(0.9825);
	TNT1 A 1 A_ZoomFactor(0.989);
	TNT1 A 1 A_ZoomFactor(0.9926);
	TNT1 A 1 A_ZoomFactor(0.995);
	TNT1 A 1 A_ZoomFactor(0.997);
	TNT1 A 6 A_ZoomFactor(1);
	Stop;
  Select:
	TNT1 A 0
	{
		invoker.offsx = -100;
		invoker.offsy = 100;
		A_WeaponOffset(0, 32, WOF_INTERPOLATE);
		A_OverlayPivotAlign(5, PSPA_CENTER, PSPA_CENTER);
		A_OverlayRotate(5, invoker.offsx / 3.5, WOF_INTERPOLATE);
	}
	TNT1 A 1
	{
		A_Overlay(5, "OverlayIdle");
		DCY_Raise();
		A_OverlayPivotAlign(5, PSPA_CENTER, PSPA_CENTER);
		A_OverlayRotate(5, invoker.offsx / 3.5, WOF_INTERPOLATE);
		return (invoker.offsx >= -0.1 || invoker.offsy <= 0.1) ? ResolveState("Ready") : ResolveState(null);
	}
	Goto Select+1;
  Deselect:
	TNT1 A 0
	{
		invoker.offsx = invoker.saveoffsx - 2;
		invoker.offsy = invoker.saveoffsy + 2;
	}
	TNT1 A 1
	{
		A_Overlay(5, "OverlayIdle");
		DCY_Lower();
		
		return (invoker.offsx <= -100 || invoker.offsy >= 100) ? ResolveState("DeselectLoop") : ResolveState(null);
	}
	Goto Deselect+1;
  Ready:
	TNT1 A 1
	{
		A_Overlay(5, "OverlayIdle");
		A_WeaponReady();
		A_OverlayOffset(5, 0, 0, WOF_INTERPOLATE);
	}
	Loop;
  Fire:
  NormalFire:
	TNT1 A 1 A_Overlay(5, "OverlayIdle");
	TNT1 A 19
	{
		A_Overlay(10, "Feel");
		DCY_Muzzle(0.8);
		A_Overlay(5, "OverlayFire");
		A_CyberaugumentedGrenade();
	}
	TNT1 A 1 A_Refire();
	Goto Ready;
  }
}