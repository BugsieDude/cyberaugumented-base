Class DCY_Liquidation : DCY_Weapon
{
  Default
  {
	Weapon.Selectionorder 2500;
	Weapon.AmmoUse1 1;
	Weapon.AmmoGive 40;
	Weapon.AmmoType1 "DCY_Battery";
	Weapon.Kickback 100;
	Weapon.SlotNumber 7;
	Height 20;
	Inventory.Pickupmessage "Aquired the 'Aurum-powered 'Liquidation' Laser Blaster 777'! It says what it's written on the tin. It liquidates everywhing.";
	Inventory.PickupSound "dcy/bigfuckinggunpickup";
	Tag "Aurum-powered 'Liquidation' Laser Blaster 777";
	Obituary "All of %o's remains was blasted by %k's Liquidation.";
	+WEAPON.BFG;
	+WEAPON.NOAUTOFIRE;
	+WEAPON.NOAUTOAIM;
	//Decal BFGLightning;
  }
  
  protected int power;
  
  action void A_Liquidation()
  {
	A_FireBullets(0, 0, 1, 0, "DCY_Null2", 0);
	DCY_Beam("ffc219", frandom(.5, 1), frandom(7, 9.5), TRF_THRUACTORS);
	A_RailAttack(45, 0, 0, "", "", RGF_SILENT|RGF_FULLBRIGHT, pufftype: "DCY_LiquidationPuff", sparsity: 8192, spawnclass: "DCY_Null");
	invoker.DepleteAmmo(invoker.bAltFire, true);
	A_GunFlash();
  }
  
  States
  {
  Spawn:
	L1QU Z -1;
	Stop;
  OverlayIdle:
	L1QU A 1;
	Loop;
  OverlayCharge:
	L1QU ABCDEF 2;
	Stop;
  OverlayFire:
	L1QU GHI 1
	{
		invoker.lower = frandom(7, 16);
		A_WeaponOffset(0 - (invoker.lower / 11), 32 + (invoker.lower), WOF_INTERPOLATE);
		DCY_HandleScale(5);
	}
	Loop;
  OverlaySteam:
	L1QU JJKKKKKKKKKK 1
	{
		if (invoker.lower > 0.01) invoker.lower *= 0.8; else invoker.lower = 0;
		A_WeaponOffset(0 - (invoker.lower / 11), 32 + (invoker.lower), WOF_INTERPOLATE);
		DCY_HandleScale(5);
	}
	L1QU K 0 A_StartSound("Liquidation/Cool", CHAN_WEAPON, volume: 0.7);
	L1QU LLLLMMMMNNNNOOOOPPPPQQQQRRRRSSSS 1
	{
		if (invoker.lower > 0.01) invoker.lower *= 0.8; else invoker.lower = 0;
		A_WeaponOffset(0 - (invoker.lower / 11), 32 + (invoker.lower), WOF_INTERPOLATE);
		DCY_HandleScale(5);
	}
	Stop;
  Feel:
	TNT1 A 1 A_ZoomFactor(frandom(0.9, 0.96));
	TNT1 A 1 A_ZoomFactor(0.9775);
	TNT1 A 1 A_ZoomFactor(0.9825);
	TNT1 A 1 A_ZoomFactor(0.9926);
	TNT1 A 1 A_ZoomFactor(0.995);
	TNT1 A 1 A_ZoomFactor(0.997);
	TNT1 A 1 A_ZoomFactor(1);
	Stop;
  Select:
	TNT1 A 0
	{
		invoker.offsx = 100;
		invoker.offsy = 150;
		A_WeaponOffset(0, 32, WOF_INTERPOLATE);
		A_OverlayPivotAlign(5, PSPA_CENTER, PSPA_CENTER);
		A_OverlayRotate(5, -(invoker.offsy / 3.5), WOF_INTERPOLATE);
	}
	TNT1 A 1
	{
		A_Overlay(5, "OverlayIdle");
		DCY_Raise(0.4);
		A_OverlayPivotAlign(5, PSPA_CENTER, PSPA_CENTER);
		A_OverlayRotate(5, -(invoker.offsy / 3.5), WOF_INTERPOLATE);
		return (invoker.offsx <= 0.1 || invoker.offsy <= 0.1) ? ResolveState("Ready") : ResolveState(null);
	}
	Goto Select+1;
  Deselect:
	TNT1 A 0
	{
		invoker.offsx = invoker.saveoffsx + 2;
		invoker.offsy = invoker.saveoffsy + 2;
		A_StopSounds(14, 15);
	}
	TNT1 A 1
	{
		A_Overlay(5, "OverlayIdle");
		DCY_Lower(1.75, 1.2);
		
		return (invoker.offsx >= 150 || invoker.offsy >= 150) ? ResolveState("DeselectLoop") : ResolveState(null);
	}
	Goto Deselect+1;
  Ready:
	TNT1 A 1
	{
		invoker.lower = 0;
		invoker.power = 0;
		A_Overlay(5, "OverlayIdle");
		A_WeaponReady();
		A_OverlayOffset(5, 0, 0, WOF_INTERPOLATE);
	}
	Loop;
  Fire:
  NormalFire:
	TNT1 A 1
	{
		A_StartSound("Liquidation/Start", 12);
		invoker.lower = 1;
		for (int i = random(10, 20); i > 0; i--)
			A_SpawnParticleEx(0xfc8403, TexMan.CheckForTexture("graphics/particles/dcysquarep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_RELATIVE, 50, random(1, 6), 0, frandom(-1, 1) + 30, frandom(-1, 1), frandom(-1, 1) + 25, frandom(-0.1, 0.1), frandom(-0.1, 0.1), frandom(-0.1, 0.1), frandom(-0.4, 0.4), frandom(-0.4, 0.4), frandom(-0.4, 0.4), fadestepf: 0, sizestep: -0.175);	
	}
	TNT1 AAAAAAAAAAAAAAAAAAAAA 1
	{
		if (invoker.lower > 0.1) invoker.lower *= 0.9; else invoker.lower = 0;
		A_WeaponOffset(frandom(-3, 3) * (invoker.lower), 32 + (frandom(-3, 3) * (invoker.lower)), WOF_INTERPOLATE);
	}
	TNT1 A 12 A_Overlay(5, "OverlayCharge");
	TNT1 A 1
	{
		A_Overlay(10, "Feel");
		A_StartSound("Liquidation/Fire", 14, CHANF_OVERLAP, 0.5);
		A_StartSound("Liquidation/Heat", 15, CHANF_LOOPING|CHANF_OVERLAP, 0.5);
		A_StartSound("Liquidation/Laser", 15, CHANF_LOOPING|CHANF_OVERLAP, 0.5);
		A_StartSound("Liquidation/Spin", 15, CHANF_LOOPING|CHANF_OVERLAP, 0.5);
		A_StartSound("Liquidation/Loop", 15, CHANF_LOOPING|CHANF_OVERLAP, 0.5);
		A_Overlay(5, "OverlayFire");
	}
	Goto FireLoop;
  FireLoop:
	TNT1 A 1
	{
		A_Overlay(10, "Feel");
		if (invoker.power < 41) invoker.power++;
		A_Liquidation();
	}
	TNT1 A 1 A_Refire("FireLoop");
	TNT1 A 0
	{ 
		A_StopSound(15);
		A_StartSound("Liquidation/SpinStop", 16);
		
		if (invoker.power < 40) return ResolveState("Ready");
		
		A_Overlay(5, "OverlaySteam");
		
		return ResolveState(null);
	}
	TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 A_WeaponReady(WRF_NOFIRE);
	Goto Ready;
  }
}

Class DCY_LiquidationTrail : Actor
{
  override void PostBeginPlay()
  {
	if (!random(0, 20)) A_SpawnParticleEx(0xfc8403, TexMan.CheckForTexture("graphics/Particles/dcylightning.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_NOTIMEFREEZE|SPF_RELATIVE|SPF_ROLL, 1, random(80, 120), 0, -frandom(20, 40), frandom(-30, 30), frandom(-30, 30), sizestep: 0.75, startroll: frandom(0, 360));
	A_SpawnParticleEx(0xffe261, TexMan.CheckForTexture("graphics/Particles/dcyglowp.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_NOTIMEFREEZE|SPF_RELATIVE, 1, random(50, 65), 0, frandom(-3, 3), frandom(-3, 3), frandom(-3, 3), sizestep: 0.75);
	A_SpawnParticleEx(0xfc8403, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_NOTIMEFREEZE|SPF_RELATIVE|SPF_ROLL, 4, random(45, 50), 0, frandom(-3, 3), frandom(-3, 3), frandom(-3, 3), frandom(-0.5, 0.5), frandom(-0.5, 0.5), frandom(-0.5, 0.5), sizestep: 2.25, startroll: frandom(0, 360), rollvel: frandom(-7, 7));
	Destroy();
  }
}

Class DCY_LiquidationPuff : BulletPuff
{
  const DCY_BALLISTA_RANGE = 256;
  
  Default { +ALWAYSPUFF }
  
  override void PostBeginPlay()
  {
	A_SpawnParticleEx(0xfc8403, TexMan.CheckForTexture("graphics/Particles/dcyglowp.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_NOTIMEFREEZE|SPF_RELATIVE, 5, random(150, 200), 0, fadestepf: 0.2, sizestep: -10);
	
	for (int i = random(8, 13); i > 0; i--)
	{
		A_SpawnParticleEx(0xfc8403, TexMan.CheckForTexture("graphics/particles/dcysquarep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_NOTIMEFREEZE|SPF_RELATIVE, 80, random(1, 9), 0, frandom(-1, 1), frandom(-1, 1), frandom(-1, 1), frandom(-1.5, 1.5), frandom(-1.5, 1.5), frandom(-1.5, 1.5), frandom(-0.2, 0.2), frandom(-0.2, 0.2), frandom(-0.2, 0.2), fadestepf: 0, sizestep: -0.075);	
		A_SpawnParticleEx(0xfc8403, TexMan.CheckForTexture("graphics/Particles/dcysmokep.png"), STYLE_ADD, SPF_FULLBRIGHT|SPF_NOTIMEFREEZE|SPF_RELATIVE|SPF_ROLL, 150, random(50, 70), frandom(0, 360), velx: frandom(-1.5, 1.5), vely: frandom(-1.5, 1.5), velz: frandom(-1.5, 1.5), fadestepf: 0.0275, sizestep: frandom(2.00, 5.25), startroll: frandom(0, 360));
	}
		
	Destroy();
  }
}