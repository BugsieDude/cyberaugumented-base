Class DCY_PumpActionShotgun : DCY_Weapon
{
  Default
  {
	Tag "Ocruistan 920 Pump-Action Shotgun";
	Inventory.PickupMessage "Grasped the Vertruistan 920 Pump-Action Shotgun.";
	Inventory.PickupSound "dcy/shotgunpickup2";
	Weapon.AmmoGive 8;
	Weapon.AmmoUse 1;
	Weapon.AmmoType "DCY_Shell";
	Weapon.SlotNumber 3;
	Weapon.SelectionOrder 1300;
  }
  
  action void A_Shotgun()
  {
	A_StartSound("DCYShotgun/Fire", CHAN_WEAPON, CHANF_OVERLAP);
	invoker.DepleteAmmo(invoker.bAltFire, true);
	A_GunFlash();
	A_FireBullets(4, 3, 7, 12, "DCY_Null", FBF_NORANDOM, 8192, "DCY_TracerPlayer", 0);
  }
  
  States
  {
  Spawn:
	SG__ Z -1;
	Stop;
  OverlayIdle:
	SG__ A 1;
	Stop;
  OverlayFire:
	SG__ A 1
	{
		invoker.lower += 4.65;
		A_WeaponOffset(0 - (invoker.lower / 15), 32 + (invoker.lower / 2), WOF_INTERPOLATE);
		DCY_HandleScale(5);
	}
	SG__ A 1
	{
		invoker.lower += 6.65;
		A_WeaponOffset(0 - (invoker.lower / 15), 32 + (invoker.lower / 2), WOF_INTERPOLATE);
		DCY_HandleScale(5);
	}
	SG__ AAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1
	{
		if (invoker.lower > 0.01) invoker.lower *= 0.45; else invoker.lower = 0;
		A_WeaponOffset(0 - (invoker.lower / 15), 32 + invoker.lower, WOF_INTERPOLATE);
		DCY_HandleScale(5);
	}
	Stop;
  OverlayReload:
	SG__ AABBCCDD 1
	{
		invoker.lower += 1;
		A_WeaponOffset(0 - (invoker.lower / 15), 32 + (invoker.lower / 3), WOF_INTERPOLATE);
		DCY_HandleScale(5);
	}
	SG__ DDDDDDDD 1
	{
		A_WeaponOffset(0 - (invoker.lower / 15), 32 + (invoker.lower / 3), WOF_INTERPOLATE);
		DCY_HandleScale(5);
	}
	SG__ DDCCBBAA 1
	{
		invoker.lower -= 1;
		A_WeaponOffset(0 - (invoker.lower / 15), 32 + (invoker.lower / 3), WOF_INTERPOLATE);
		DCY_HandleScale(5);
	}
	SG__ A 2;
	Stop;
  OverlayMuzzle:
	MUZ_ AA 1 Bright
	{
		invoker.t += 0.2;
		A_OverlayRotate(-1, frandom(0, 360));
		A_OverlayScale(-1, invoker.t, invoker.t);
	}
	Stop;
  Feel:
	TNT1 A 1 A_ZoomFactor(0.9);
	TNT1 A 1 A_ZoomFactor(0.9675);
	TNT1 A 1 A_ZoomFactor(0.9825);
	TNT1 A 1 A_ZoomFactor(0.9926);
	TNT1 A 1 A_ZoomFactor(0.995);
	TNT1 A 1 A_ZoomFactor(0.997);
	TNT1 A 1 A_ZoomFactor(1);
	Stop;
  Select:
	TNT1 A 0
	{
		invoker.offsx = 150;
		invoker.offsy = 150;
		A_WeaponOffset(0, 32, WOF_INTERPOLATE);
		A_OverlayPivotAlign(5, PSPA_CENTER, PSPA_CENTER);
		A_OverlayRotate(5, (invoker.offsy / 3.5), WOF_INTERPOLATE);
	}
	TNT1 A 1
	{
		A_Overlay(5, "OverlayIdle");
		DCY_Raise(0.4);
		A_OverlayPivotAlign(5, PSPA_CENTER, PSPA_CENTER);
		A_OverlayRotate(5, -(invoker.offsy / 3.5), WOF_INTERPOLATE);
		return (invoker.offsx <= 0.1 || invoker.offsy <= 0.1) ? ResolveState("Ready") : ResolveState(null);
	}
	Goto Select+1;
  Deselect:
	TNT1 A 0
	{
		invoker.offsx = invoker.saveoffsx + 2;
		invoker.offsy = invoker.saveoffsy + 2;
	}
	TNT1 A 1
	{
		A_Overlay(5, "OverlayIdle");
		DCY_Lower(1.75, 1.05);
		
		return (invoker.offsx >= 150 || invoker.offsy >= 150) ? ResolveState("DeselectLoop") : ResolveState(null);
	}
	Goto Deselect+1;
  Ready:
	TNT1 A 1
	{
		invoker.lower = 0;
		A_Overlay(5, "OverlayIdle");
		A_WeaponReady();
		A_OverlayOffset(5, 0, 0, WOF_INTERPOLATE);
	}
	Loop;
  Fire:
  NormalFire:
	TNT1 A 10
	{
		A_Overlay(10, "Feel");
		DCY_Muzzle(0.4);
		A_Overlay(5, "OverlayFire");
		A_Shotgun();
	}
	TNT1 A 0 A_JumpIfNoAmmo("Ready");
	TNT1 A 24 A_Overlay(5, "OverlayReload");
	TNT1 A 1
	{
		A_Refire();
	}
	Goto Ready;
  }
}